plugins {
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

apply from: 'build-quality.gradle'
apply from: 'release.gradle'

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "rally"
    pluginsDir = "$buildDir/plugins"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://m2proxy.atlassian.com/repository/public" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }


}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'EPAM Report portal. Rally Integration Plugin'

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api:5.0.0-BETA-6'
        annotationProcessor 'com.epam.reportportal:plugin-api:5.0.0-BETA-6'
        compile 'com.epam.reportportal:commons-model:5.0.0-BETA-6'

    } else {
        implementation 'com.github.reportportal:plugin-api:a60d458'
        annotationProcessor 'com.github.reportportal:plugin-api:a60d458'
        compile 'com.github.reportportal:commons-model:5.0.0-BETA-6'

    }

    compile group: 'com.rallydev.rest', name: 'rally-rest-api', version: '2.2.1'
    
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile 'org.jasypt:jasypt:1.9.2'

    compile 'net.oauth.core:oauth:20090617'
    compile 'net.oauth.core:oauth-httpclient4:20090913'

    compile 'org.apache.tika:tika-core:1.14'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.6'
    
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    testCompile group: 'com.google.guava', name: 'guava', version: '23.0'

}

test {
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

build.dependsOn jacocoTestReport

wrapper {
    gradleVersion = '5.4.1'
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Andrei Varabyeu",
                "Plugin-Class": "com.epam.reportportal.extension.bugtracking.rally.RallyStrategyPlugin",
                "Plugin-Service": "api"
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    zip64 true
    dependencies {
        include(dependency('com.google.guava:guava'))
        include(dependency('com.rallydev.rest:rally-rest-api'))
    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}
